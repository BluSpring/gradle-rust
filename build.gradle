plugins {
    id("java-gradle-plugin")
    id("maven-publish")
    id("com.gradle.plugin-publish") version "0.15.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = "fr.stardustenterprises"
archivesBaseName = "rust"
version = "2.0.0"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly(gradleApi())
    implementation("commons-io:commons-io:2.11.0")
    implementation("net.lingala.zip4j:zip4j:2.9.0")
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from "LICENSE"
}

gradlePlugin {
    plugins {
        wrapper {
            id = "fr.stardustenterprises.rust.wrapper"
            implementationClass = "fr.stardustenterprises.rust.wrapper.WrapperPlugin"
            displayName = "Rust Wrapper"
            description = "A plugin that wraps Rust's build systems, for embedding Rust libraries in Java projects."
        }
        importer {
            id = "fr.stardustenterprises.rust.importer"
            implementationClass = "fr.stardustenterprises.rust.importer.ImporterPlugin"
            displayName = "Rust Importer"
            description = "A plugin that makes it possible to import outputs from Rust from another Gradle project."
        }
    }
}

pluginBundle {
    website = "https://github.com/stardust-enterprises/gradle-rust"
    vcsUrl = "https://github.com/stardust-enterprises/gradle-rust.git"
    tags = ['rust', 'cargo', 'native']
}

afterEvaluate {
    publishing {
        publications {
            forEach {
                if(it instanceof MavenPublication) {
                    it.pom {
                        url = "https://github.com/stardust-enterprises/gradle-rust"
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'xtrm'
                                name = "xTrM_"
                            }
                            developer {
                                id = 'arcblroth'
                                name = "Arc'blroth"
                            }
                        }
                        scm {
                            def scmRepo = "scm:git:git://github.com/stardust-enterprises/gradle-rust.git"
                            connection = scmRepo
                            developerConnection = scmRepo
                            url = scmRepo
                        }
                    }
                }
            }
        }
    }
}
