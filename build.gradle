plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.15.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'fr.stardust.rust'
archivesBaseName = 'cross-wrapper'

dependencies {
    compileOnly gradleApi()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from 'LICENSE'
}

pluginBundle {
    website = "https://$pluginUrlBase"
    vcsUrl = "https://${pluginUrlBase}.git"
    tags = ['rust', 'cargo', 'cross', 'native']
}

gradlePlugin {
    plugins {
        crossWrapper {
            id = 'io.github.stardustenterprises.cross'
            implementationClass = 'io.github.stardustenterprises.cross.CrossWrapperPlugin'
            displayName = pluginDisplayName
            description = pluginDescription
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            forEach {
                if(it instanceof MavenPublication) {
                    it.pom {
                        url = "https://$pluginUrlBase"
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'arcblroth'
                                name = "Arc'blroth"
                            }
                            developer {
                                id = 'xtrm'
                                name = "xtrm"
                            }
                        }
                        scm {
                            def scmRepo = "scm:git:git://${pluginUrlBase}.git"
                            connection = scmRepo
                            developerConnection = scmRepo
                            url = scmRepo
                        }
                    }
                }
            }
        }
    }
}
